---
title: "Project"
author: "Yuying Wang Yehao Zhang"
date: "12/5/2019"
output: html_document
---

```{r setup, include=FALSE}
library(devtools)
library(Statamarkdown)
library(RStata)
```

# Introduction
This project is aimed to answer the question __Do people with higher carbohydrate intake feel more sleepy during the day?__. According to the National Sleep Foundation, carbs could cause blood sugar to spike quickly, followed by a drop that can leave people feeling tired after eating. So, it brought up our interest to examine the specific relationship through data *NHANES 2015-2016* available online with a decent amount of sample size. 

A regression model is fit on the response variable __sleepiness__ and independent variable __carbohydrate intake ratio__, controlling for other variables found relevant from the data sets to study if there is evidence supporting a positive relationship.


# Data: 
3 data sets extracted from `https://wwwn.cdc.gov/nchs/nhanes/ContinuousNhanes/Default.aspx?BeginYear=2015`

<ul>
<li>DR1TOT_I.XPT: Dietary Interview - Total Nutrient Intakes, First Day
<ul>
<li>DR1TKCAL - Energy (kcal)</li>
<li>DR1TCARB - Carbohydrate (gm)</li>
</ul>
</li>
<li>SLQ_I.XPT: Sleep Disorders
<ul>
<li>SLD012 - Sleep hours</li>
<li>SLQ120 - How often feel overly sleepy during day?</li>
</ul>
</li>
<li>DEMO_I.XPT: Demographic Variables and Sample Weights
<ul>
<li>RIDAGEYR - Age in years at screening</li>
<li>RIAGENDR - Gender</li>
<li>RIDEXMON - Six month time period</li>
</ul>
</li>

* DR1TOT_I.XPT: Dietary Interview - Total Nutrient Intakes, First Day
  + DR1TKCAL - Energy (kcal)
  + DR1TCARB - Carbohydrate (gm)
* SLQ_I.XPT: Sleep Disorders
  + SLD012 - Sleep hours
  + SLQ120 - How often feel overly sleepy during day?
* DEMO_I.XPT: Demographic Variables and Sample Weights
  + RIDAGEYR - Age in years at screening
  + RIAGENDR - Gender
  + RIDEXMON - Six month time period

# Methods
Three datasets are merged together by the respondent id. An independent variable indicating the carbohydrate intake ratio is created through $DR1TCARB*4/DR1TKCAL$, where 4 represents the unit conversion from gram to kcal for carbohydrate. Then based on the suggested Carbohydrate intake from the Dietary Guidelines for Americans, which is 45 to 65 percent of total diet, a factor variable is created by assigning 0 to those ratio that fall in this group, 1 to those ratio lower than the suggested level, and 2 tothose ratio higher than the suggested level.

Since our response variable __SLQ120__ is an order factor with the lowest value representing Rarely and the highest level representing Almost always, we use ordinal logistic regression to fit the model in R and Stata. 

# Core Analysis
## Stata code
```{stata}
* import data and keep and rename relevant variables
* import total nutrient day 1 2015
fdause DR1TOT_I.XPT, clear
quietly compress
gsort +seqn
generate carb_ratio = dr1tcarb*4 / dr1tkcal
* 0 for carb_ratio between 0.45 and 0.65
generate carb_level = 0
replace carb_level = 1 if carb_ratio < 0.45
replace carb_level = 2 if carb_ratio > 0.65
keep seqn carb_level
save DR1TOT_l.dta, replace

* import sleep disorder data
fdause SLQ_I.XPT, clear
quietly compress
gsort +seqn
keep seqn sld012 slq120
* drop 'dont know' response
drop if slq120 == 9
rename sld012 sleephr
rename slq120 sleepy
merge 1:1 seqn using DR1TOT_l.dta
drop _merge
save DRTOT_SLP.dta, replace

* import demographic data
fdause DEMO_I.XPT, clear
quietly compress
gsort +seqn
generate winter = 0
replace winter = 1 if ridexmon == 1
* 0 male 1 female
generate gender = riagendr - 1
keep seqn gender ridageyr winter
rename ridageyr age
merge 1:1 seqn using DRTOT_SLP.dta
keep if _merge == 3
drop _merge
label define sleepiness 0 "Never" 1 "Rarely" 2 "Sometimes" 3 "Often" 4 "Almost always"
label values sleepy sleepiness
save DRTOT_SLP_DEMO.dta, replace

* response: sleepy (five levels: 0-4)
* independent variables: carb_ratio
* control variables: gender, age, winter, sleephr

* ordered logistic regression (proportional odds assumption)
ologit sleepy i.carb_level sleephr gender age winter

* intall package for brant test to check assumption
net from http://www.indiana.edu/~jslsoc/stata/
net install spost13_ado
brant

* assumption for ologit violated so try generalized
ssc install gologit2

* compare between models
quietly gologit2 sleepy i.carb_level sleephr gender age winter, store(gologit)
* Partial Proportional Odds Model (constraints from brant test)
quietly gologit2 sleepy i.carb_level sleephr gender age winter, store(gologit2) pl(age i.carb_level gender)
quietly gologit2 sleepy i.carb_level sleephr gender age winter, store(ologit) pl
* ologit is too restrictive
lrtest ologit gologit
* if use significance level 0.001, partial proportional odds is not too restrictive
lrtest gologit gologit2

* model we chose
quietly gologit2 sleepy i.carb_level sleephr gender age winter, store(gologit2) pl(age i.carb_level gender)

* coefficient table
esttab gologit2, wide label title(Rgression Table for Partial Proportional Odds Model) addnote("Source: DRTOT_SLP_DEMO.dta")

* marginal effect for carb_level
mtable, dydx(carb_level)
```


# Additional Analysis

# Results

